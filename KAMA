//KAMA moving average code.
//카우프만 이동평균선
//original code from everget
//에버젯의 오리지널 코드를 버젼4로 컨버트 한 코드입니다.

//@version=4
// This source code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © Mby_lab

study("Kaufman Adaptive Moving Average", shorttitle="KAMA", overlay=true)

length = input(title="Length", type=input.integer, defval=14)
fastLength = input(title="Fast EMA Length", type=input.integer, defval=2)
slowLength = input(title="Slow EMA Length", type=input.integer, defval=30)
highlightMovements = input(title="Highlight Movements ?", type=input.bool, defval=true)
src = input(title="Source", type=input.source, defval=close)

mom = abs(change(src, length))
volatility = sum(abs(change(src)), length)

// Efficiency Ratio
er = volatility != 0 ? mom / volatility : 0

fastAlpha = 2 / (fastLength + 1)
slowAlpha = 2 / (slowLength + 1)

// KAMA Alpha
sc = pow(er * (fastAlpha - slowAlpha) + slowAlpha, 2)

kama = 0.0
kama := sc * src + (1 - sc) * nz(kama[1])

kamaColor = highlightMovements ? kama > kama[1] ? color.green : color.red : #6d1e7f
plot(kama, title="KAMA", linewidth=2, color=kamaColor, transp=0)
